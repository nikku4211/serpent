ct SF[4] pvelx_table = SF[4](0, 0.1, 0, -0.1)
ct SF[4] pvely_table = SF[4](0.1, 0, -0.1, 0)
//                          (downdown, downright, downup, downleft,
//                          rightdown, rightright, rightup, rightleft,
//                          updown, upright, upup, upleft,
//                          leftdown, leftright, leftup, leftleft)
ct U[16] ptile_table = U[16](12, 16, 12, 17, 
                             15, 7, 17, 7, 
                             12, 14, 12, 15, 
                             14, 7, 16, 7)
ct U[4] rtile_table = U[4](20, 19, 21, 18)
ct SF[4] rvelx_table = SF[4](0, 0.1, 0, -0.1)
ct SF[4] rvely_table = SF[4](0.1, 0, -0.1, 0)

vars /serp_vars
    UF px = 12
    UF py = 10
    UF opx = 12
    UF opy = 10
    U pleat = 0
    U[256] psnpx
    U[256] psnpy
    U psnpxbi = 0
    U psnpybi = 0
    U pdir = 1
    U opdir = 1
    U plength = 5
    U pheadt = 4
    UU pheadp = 0
    U pbodyt = 3
    UU pbodyp = 0
    U ptailt = 1
    UU ptailp = 0
    UU optailp = 0
    U pcornt = 4
    U optailt_counter = 7
    U optailt = 5
    Bool pturn = false
    UF rx = 16
    UF ry = 15
    UU rtp = 0
    UF orx = 16
    UF ory = 15
    UU ortp = 0
    U rdir = 3
    SF rvmul = 1
    U rtele = 0
    U ortele = 0
    U score = 0
    Bool lost = false
    U shame_counter = 120
    U shame_scroll = 0
    UU wintro_counter = 384
    U chr_anim_counter = 12
    U chr_frame = 0

data /serp_data
    [] game_back_nt
        file(raw, "gamebkg.nam")
    [] title_back_nt
        file(raw, "title.nam")
    [] win_back_nt
        file(raw, "win.nam")
    [] lose1_back_nt
        file(raw, "lose1.nam")
    [] lose2_back_nt
        file(raw, "lose2.nam")
ct U [25] nexxt_default = file(raw, "nexxtdefaultnf.pal")

fn load_nametable(CCC/serp_data nametable)
    ppu_reset_addr($2000)
    for UU i = 0; i < 960; i += 1
        {PPUDATA}(nametable{i})
    for UU i = 0; i < 64; i += 1
        {PPUDATA}(nametable{i+960})

fn load_nametable_tall()
    ppu_reset_addr($2000)
    for UU i = 0; i < 960; i += 1
        {PPUDATA}(@lose1_back_nt{i})
    for UU i = 0; i < 64; i += 1
        {PPUDATA}(@lose1_back_nt{i+960})
    ppu_set_addr($2800)
    for UU i = 0; i < 960; i += 1
        {PPUDATA}(@lose2_back_nt{i})
    for UU i = 0; i < 64; i += 1
        {PPUDATA}(@lose2_back_nt{i+960})

fn init_arrays()
    //clear the entire 256-byte array...
    for U i = 0; i < 255; i += 1
        psnpx[i] = 0
    for U i = 0; i < 255; i += 1
        psnpy[i] = 0
    
    //...even the 256th byte
    psnpx[255] = 0
    psnpy[255] = 0

    psnpx[0] = px.a
    psnpy[0] = py.a
    psnpxbi = 1
    psnpybi = 1

fn init_game_vars()
    px = 12
    py = 10
    opx = 12
    opy = 10
    pleat = 0
    
    psnpxbi = 0
    psnpybi = 0
    pdir = 1
    opdir = 1
    
    plength = 5
    pheadt = 8
    pheadp = 0
    pbodyt = 7
    pbodyp = 0
    ptailt = 5
    ptailp = 0
    optailp = 0
    pcornt = 7
    
    optailt_counter = 7
    optailt = 5
    pturn = false
    rx = 16
    ry = 15
    rtp = 0
    orx = 16
    ory = 15
    ortp = 0
    
    rdir = 3
    rvmul = 1
    rtele = 0
    ortele = 0
    score = 0

nmi main_nmi()
    //update oam and poll controller presses
    ppu_upload_oam_poll_pads(0)

    //turn rendering on
    {PPUMASK}(PPUMASK_ON | PPUMASK_NO_CLIP)

    update_bkg()

    //reset scroll
    ppu_reset_scroll(0, 0)

    if chr_anim_counter > 0
        chr_anim_counter -= 1
    else if chr_anim_counter == 0
        switch chr_frame
            case 0
                {PPUCTRL}(PPUCTRL_NMI_ON)
                chr_frame = 1
                break
            case 1
                {PPUCTRL}(PPUCTRL_NMI_ON | PPUCTRL_BG_PT_1000)
                chr_frame = 0
                
        chr_anim_counter = 12

    puf.process(PUF_DEFAULT)

mode game()
: nmi main_nmi
    
    // turn NMI off
    {PPUCTRL}(0)
    //turn rendering off
    {PPUMASK}(0)

    puf.play_track(puf_track_game)

    // load the level
    load_nametable(@game_back_nt)

    init_game_vars()

    init_arrays()

    set_rat()

    // have NMI on every frame
    {PPUCTRL}(PPUCTRL_NMI_ON)

    puf.play_sfx(0)

    while true
        update_pads()
        move_player()
        move_rat()
        eat_check()
        if score == 20
            goto mode win()
            : preserves
        if lost
            goto mode lose()
            : preserves
        tail_check()
        nmi

nmi win_nmi()
    //update oam and poll controller presses
    ppu_upload_oam_poll_pads(0)

    //turn rendering on
    {PPUMASK}(PPUMASK_ON | PPUMASK_NO_CLIP)

    //reset scroll
    ppu_set_scroll(0, shame_scroll)
    
    puf.process(PUF_DEFAULT)

mode win()
: nmi win_nmi
    // turn NMI off
    {PPUCTRL}(0)
    //turn rendering off
    {PPUMASK}(0)

    load_nametable(@win_back_nt)

    puf.play_track(puf_track_wintro)

    // have NMI on every frame
    {PPUCTRL}(PPUCTRL_NMI_ON)

    while true
        update_pads()
        pads_menu()
        if wintro_counter > 0 && wintro_counter < 385
            wintro_counter -= 1
        if wintro_counter == 0
            wintro_counter = 385
            puf.play_track(puf_track_win)

        nmi

fn tail_check()

    //set positions before NMI
    optailp = $2000+(UU(psnpy[psnpybi-plength])<<5)+UU(psnpx[psnpxbi-plength])
    ptailp = $2000+(UU(psnpy[psnpybi-(plength-1)])<<5)+UU(psnpx[psnpxbi-(plength-1)])
    pbodyp = $2000+(UU(opy.a)<<5)+UU(opx.a)
    pheadp = $2000+(UU(py.a)<<5)+UU(px.a)

    rtp = $2000+(UU(ry.a)<<5)+UU(rx.a)
    ortp = $2000+(UU(ory.a)<<5)+UU(orx.a)

fn update_bkg()   

    //read the snake head on the background
    fence
    ppu_set_addr(pheadp)
    pleat = {PPUDATA}()
    pleat = {PPUDATA}()
    fence

    //read the rat on the background
    fence
    ppu_set_addr(rtp)
    rtele = {PPUDATA}()
    rtele = {PPUDATA}()
    fence

    ouro_check()

    //draw a black space over the old rat on the background
    ppu_set_addr(ortp)
    {PPUDATA}(ortele)

    if opx.a != px.a || opy.a != py.a
        //draw the snake body on the background
        ppu_set_addr(pbodyp)
        {PPUDATA}(ptile_table[pbodyt])

        //draw the snake head on the background
        ppu_set_addr(pheadp)
        {PPUDATA}(pheadt)

        //draw a blank space over the snake tail on the background
        ppu_set_addr(optailp)
        {PPUDATA}(0)

        //draw the snake tail on the background
        ppu_set_addr(ptailp)
        {PPUDATA}(optailt)

    //draw the rat on the background
    ppu_set_addr(rtp)
    {PPUDATA}(rtile_table[rdir])

    ppu_set_addr($2096)
    {PPUDATA}(u_to_dd(score)[1] + $30)
    {PPUDATA}(u_to_dd(score)[0] + $30)

    if (orx.a != rx.a || ory.a != ry.a)
        ortele = rtele

fn move_player()

    if (px.a != opx.a) || (py.a != opy.a)
        if optailt_counter == 0
            optailt = ptailt
            optailt_counter = plength
        opdir = pdir

    //move snake head horizontally
    if pads[0].held & BUTTON_DPAD == BUTTON_LEFT && (pdir == 0 || pdir == 2)
        pdir = 3
        pheadt = 6
        ptailt = 9
    else if pads[0].held & BUTTON_DPAD == BUTTON_RIGHT && (pdir == 0 || pdir == 2)
        pdir = 1
        pheadt = 8
        ptailt = 5
    //move snake head vertically
    else if pads[0].held & BUTTON_DPAD == BUTTON_UP && (pdir == 1 || pdir == 3)
        pdir = 2
        pheadt = 13
        ptailt = 10
    else if pads[0].held & BUTTON_DPAD == BUTTON_DOWN && (pdir == 1 || pdir == 3)
        pdir = 0
        pheadt = 11
        ptailt = 4

    if pads[0].held & BUTTON_SELECT
        goto mode win()
        : preserves

    pbodyt = (opdir<<2)+pdir

    opx = px
    opy = py

    //make sure snake is always moving
    px += pvelx_table[pdir]
    py += pvely_table[pdir]

    //wrap snake around screen
    if py < 6
        py = 25
    else if py > 26
        py = 6
    
    if px < 2
        px = 29
    else if px > 30
        px = 2

    if (px.a != opx.a) || (py.a != opy.a)
        if optailt_counter > 0
            optailt_counter -= 1
        //store head position at turn
        psnpx[psnpxbi] = px.a
        psnpy[psnpybi] = py.a
        //increment snake buffer index
        psnpxbi += 1
        psnpybi += 1
        if pdir != opdir
            pturn = true
            if (opdir == 3 && pdir == 0) || (opdir == 0 && pdir == 3)  //left down
                pcornt = 10
            else if (opdir == 3 && pdir == 2) || (opdir == 2 && pdir == 3)  //left up
                pcornt = 12
            else if (opdir == 1 && pdir == 0) || (opdir == 0 && pdir == 1)  //right down
                pcornt = 11
            else if (opdir == 1 && pdir == 2) || (opdir == 2 && pdir == 1)  //right up
                pcornt = 13
        else
            pturn = false

fn set_rat()
    //set the old rat position
    //orx = rx
    //ory = ry

    //put rat in random place
    rx = randb(27)
    ry = randb(19)

    //in bounds
    rx += 2
    ry += 6

    if rtele == 1
        //put rat in random place
        rx = randb(27)
        ry = randb(19)

        //in bounds
        rx += 2
        ry += 6
        

fn move_rat()
    orx = rx
    ory = ry
    
    rvmul = (SF(score+1)>>5)

    //rat is always moving
    rx += rvelx_table[rdir] * rvmul
    ry += rvely_table[rdir] * rvmul

    //wrap rat around screen
    if ry < 6
        ry = 25
    else if ry > 26
        ry = 6
    
    if rx < 2
        rx = 29
    else if rx > 30
        rx = 2
    
    if rtele == 1
        score += 1
        plength += 1
        puf.play_sfx(1)
        set_rat()

fn eat_check()
    if px.a == rx.a && py.a == ry.a
        score += 1
        plength += 1
        puf.play_sfx(1)
        set_rat()

fn ouro_check()
    if (pleat <= 17 && pleat >= 4) && (px.a != opx.a || py.a != opy.a)
        puf.play_sfx(2)
        lost = true

mode lose()
: nmi win_nmi

    // turn NMI off
    {PPUCTRL}(0)
    //turn rendering off
    {PPUMASK}(0)

    load_nametable_tall()

    // have NMI on every frame
    {PPUCTRL}(PPUCTRL_NMI_ON)

    shame_scroll = 0
    shame_counter = 120

    ppu_reset_scroll(0, 0)

    while true
        update_pads()
        pads_menu()
        if shame_counter > 0
            shame_counter -= 1
        else if shame_counter == 0 && shame_scroll < 239
            shame_scroll += 1
        nmi

mode main()
: nmi win_nmi
    // turn NMI off
    {PPUCTRL}(0)
    //turn rendering off
    {PPUMASK}(0)

    // upload palette
    palette = nexxt_default
    ppu_upload_palette()

    load_nametable(@title_back_nt)

    init_arrays()

    // Clear the remainder of OAM
    hide_oam(0)

    // have NMI on every frame
    {PPUCTRL}(PPUCTRL_NMI_ON)

    puf.init(system)
    puf.play_track(puf_track_title)

    while true
        update_pads()
        pads_menu()
        nmi

fn pads_menu()
    if pads[0].held & BUTTON_START
        goto mode game()
        : preserves /serp_vars


// Define the tileset (commonly called CHR):
chrrom
    file(fmt, "serchr.png")

audio(puf1_music, "music.txt")
audio(puf1_sfx, "sfx.txt", "sfx.nsf")